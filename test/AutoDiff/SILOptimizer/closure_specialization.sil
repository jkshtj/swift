// RUN: %target-sil-opt -test-runner %s -o /dev/null 2>&1 | %FileCheck %s

// REQUIRES: swift_in_compiler

sil_stage canonical

import Builtin
import Swift
import SwiftShims

import _Differentiation

//////////////////////////////
// Single closure call site //
//////////////////////////////
sil @$vjpMultiply : $@convention(thin) (Float, Float, Float) -> (Float, Float) 

sil private @$pullback_f : $@convention(thin) (Float, @owned @callee_guaranteed (Float) -> (Float, Float)) -> Float {
bb0(%0 : $Float, %1 : $@callee_guaranteed (Float) -> (Float, Float)):
  %2 = apply %1(%0) : $@callee_guaranteed (Float) -> (Float, Float) // users: %5, %4
  strong_release %1 : $@callee_guaranteed (Float) -> (Float, Float) // id: %3
  %4 = tuple_extract %2 : $(Float, Float), 0      // user: %7
  %5 = tuple_extract %2 : $(Float, Float), 1      // user: %6
  %6 = struct_extract %5 : $Float, #Float._value  // user: %8
  %7 = struct_extract %4 : $Float, #Float._value  // user: %8
  %8 = builtin "fadd_FPIEEE32"(%6 : $Builtin.FPIEEE32, %7 : $Builtin.FPIEEE32) : $Builtin.FPIEEE32 // user: %9
  %9 = struct $Float (%8 : $Builtin.FPIEEE32)     // users: %11, %10
  debug_value %9 : $Float, let, name "x", argno 1 // id: %10
  return %9 : $Float                              // id: %11
}

// reverse-mode derivative of f(_:)
sil hidden @$s4test1fyS2fFTJrSpSr : $@convention(thin) (Float) -> (Float, @owned @callee_guaranteed (Float) -> Float) {
bb0(%0 : $Float):
  //=========== Test callsite and closure gathering logic ===========//
  specify_test "closure_specialize_gather_call_sites"
  // CHECK-LABEL: Specializing closures in function: $s4test1fyS2fFTJrSpSr
  // CHECK: PartialApply call site:   %8 = partial_apply [callee_guaranteed] %7(%6) : $@convention(thin) (Float, @owned @callee_guaranteed (Float) -> (Float, Float)) -> Float // user: %9
  // CHECK: Passed in closures:
  // CHECK: 1.   %6 = partial_apply [callee_guaranteed] %5(%0, %0) : $@convention(thin) (Float, Float, Float) -> (Float, Float) // user: %8


 //=========== Test specialized function signature and body ===========//
  specify_test "closure_specialize_specialized_function_signature_and_body"
  // CHECK-LABEL: Generated specialized function: $s11$pullback_f12$vjpMultiplyS2fTf1nc_n
  // CHECK: sil private @$s11$pullback_f12$vjpMultiplyS2fTf1nc_n : $@convention(thin) (Float, Float, Float) -> Float {
  // CHECK: bb0(%0 : $Float, %1 : $Float, %2 : $Float):
  // CHECK: %3 = function_ref @$vjpMultiply : $@convention(thin) (Float, Float, Float) -> (Float, Float) // user: %4
  // CHECK: %4 = partial_apply [callee_guaranteed] %3(%1, %2) : $@convention(thin) (Float, Float, Float) -> (Float, Float) // users: %6, %5
  // CHECK: %5 = apply %4(%0) : $@callee_guaranteed (Float) -> (Float, Float) // users: %8, %7
  // CHECK: strong_release %4 : $@callee_guaranteed (Float) -> (Float, Float) // id: %6
  // CHECK: return

  debug_value %0 : $Float, let, name "x", argno 1 // id: %1
  %2 = struct_extract %0 : $Float, #Float._value  // users: %3, %3
  %3 = builtin "fmul_FPIEEE32"(%2 : $Builtin.FPIEEE32, %2 : $Builtin.FPIEEE32) : $Builtin.FPIEEE32 // user: %4
  %4 = struct $Float (%3 : $Builtin.FPIEEE32)     // user: %9
  // function_ref closure #1 in static Float._vjpMultiply(lhs:rhs:)
  %5 = function_ref @$vjpMultiply : $@convention(thin) (Float, Float, Float) -> (Float, Float) // user: %6
  %6 = partial_apply [callee_guaranteed] %5(%0, %0) : $@convention(thin) (Float, Float, Float) -> (Float, Float) // user: %8
  // function_ref pullback of f(_:)
  %7 = function_ref @$pullback_f : $@convention(thin) (Float, @owned @callee_guaranteed (Float) -> (Float, Float)) -> Float // user: %8
  %8 = partial_apply [callee_guaranteed] %7(%6) : $@convention(thin) (Float, @owned @callee_guaranteed (Float) -> (Float, Float)) -> Float // user: %9
  %9 = tuple (%4 : $Float, %8 : $@callee_guaranteed (Float) -> Float) // user: %10
  return %9 : $(Float, @callee_guaranteed (Float) -> Float) // id: %10
}

///////////////////////////////
// Multiple closure callsite //
///////////////////////////////
sil @$vjpSin : $@convention(thin) (Float, Float) -> Float // user: %6
sil @$vjpCos : $@convention(thin) (Float, Float) -> Float // user: %10

// pullback of g(_:)
sil private @$pullback_g : $@convention(thin) (Float, @owned @callee_guaranteed (Float) -> Float, @owned @callee_guaranteed (Float) -> Float, @owned @callee_guaranteed (Float) -> (Float, Float)) -> Float {
bb0(%0 : $Float, %1 : $@callee_guaranteed (Float) -> Float, %2 : $@callee_guaranteed (Float) -> Float, %3 : $@callee_guaranteed (Float) -> (Float, Float)):
  %4 = apply %3(%0) : $@callee_guaranteed (Float) -> (Float, Float) // users: %7, %6
  strong_release %3 : $@callee_guaranteed (Float) -> (Float, Float) // id: %5
  %6 = tuple_extract %4 : $(Float, Float), 0      // user: %10
  %7 = tuple_extract %4 : $(Float, Float), 1      // user: %8
  %8 = apply %2(%7) : $@callee_guaranteed (Float) -> Float // user: %12
  strong_release %2 : $@callee_guaranteed (Float) -> Float // id: %9
  %10 = apply %1(%6) : $@callee_guaranteed (Float) -> Float // user: %13
  strong_release %1 : $@callee_guaranteed (Float) -> Float // id: %11
  %12 = struct_extract %8 : $Float, #Float._value // user: %14
  %13 = struct_extract %10 : $Float, #Float._value // user: %14
  %14 = builtin "fadd_FPIEEE32"(%13 : $Builtin.FPIEEE32, %12 : $Builtin.FPIEEE32) : $Builtin.FPIEEE32 // user: %15
  %15 = struct $Float (%14 : $Builtin.FPIEEE32)   // users: %17, %16
  debug_value %15 : $Float, let, name "x", argno 1 // id: %16
  return %15 : $Float                             // id: %17
}

// reverse-mode derivative of g(_:)
sil hidden @$s4test1gyS2fFTJrSpSr : $@convention(thin) (Float) -> (Float, @owned @callee_guaranteed (Float) -> Float) {
bb0(%0 : $Float):
  //=========== Test callsite and closure gathering logic ===========//
  specify_test "closure_specialize_gather_call_sites"
  // CHECK-LABEL: Specializing closures in function: $s4test1gyS2fFTJrSpSr
  // CHECK: PartialApply call site:   %16 = partial_apply [callee_guaranteed] %15(%6, %10, %14) : $@convention(thin) (Float, @owned @callee_guaranteed (Float) -> Float, @owned @callee_guaranteed (Float) -> Float, @owned @callee_guaranteed (Float) -> (Float, Float)) -> Float // user: %17
  // CHECK: Passed in closures:
  // CHECK: 1.   %6 = partial_apply [callee_guaranteed] %5(%0) : $@convention(thin) (Float, Float) -> Float // user: %16
  // CHECK: 2.   %10 = partial_apply [callee_guaranteed] %9(%0) : $@convention(thin) (Float, Float) -> Float // user: %16
  // CHECK: 3.   %14 = partial_apply [callee_guaranteed] %13(%8, %4) : $@convention(thin) (Float, Float, Float) -> (Float, Float) // user: %16

  //=========== Test specialized function signature and body ===========//
  specify_test "closure_specialize_specialized_function_signature_and_body"
  // CHECK-LABEL: Generated specialized function: $s11$pullback_g7$vjpSinSf0B3CosSf0B8MultiplyS2fTf1nccc_n
  // CHECK: sil private @$s11$pullback_g7$vjpSinSf0B3CosSf0B8MultiplyS2fTf1nccc_n : $@convention(thin) (Float, Float, Float, Float, Float) -> Float {
  // CHECK: bb0(%0 : $Float, %1 : $Float, %2 : $Float, %3 : $Float, %4 : $Float):
  // CHECK: %5 = function_ref @$vjpSin : $@convention(thin) (Float, Float) -> Float // user: %6
  // CHECK: %6 = partial_apply [callee_guaranteed] %5(%1) : $@convention(thin) (Float, Float) -> Float // users: %18, %17
  // CHECK: %7 = function_ref @$vjpCos : $@convention(thin) (Float, Float) -> Float // user: %8
  // CHECK: %8 = partial_apply [callee_guaranteed] %7(%2) : $@convention(thin) (Float, Float) -> Float // users: %16, %15
  // CHECK: %9 = function_ref @$vjpMultiply : $@convention(thin) (Float, Float, Float) -> (Float, Float) // user: %10
  // CHECK: %10 = partial_apply [callee_guaranteed] %9(%3, %4) : $@convention(thin) (Float, Float, Float) -> (Float, Float) // users: %12, %11
  // CHECK: %11 = apply %10(%0) : $@callee_guaranteed (Float) -> (Float, Float) // users: %14, %13
  // CHECK: strong_release %10 : $@callee_guaranteed (Float) -> (Float, Float) // id: %12
  // CHECK: %15 = apply %8(%14) : $@callee_guaranteed (Float) -> Float // user: %19
  // CHECK: strong_release %8 : $@callee_guaranteed (Float) -> Float // id: %16
  // CHECK: %17 = apply %6(%13) : $@callee_guaranteed (Float) -> Float // user: %20
  // CHECK: strong_release %6 : $@callee_guaranteed (Float) -> Float // id: %18
  // CHECK: return  

  debug_value %0 : $Float, let, name "x", argno 1 // id: %1
  %2 = struct_extract %0 : $Float, #Float._value  // users: %7, %3
  %3 = builtin "int_sin_FPIEEE32"(%2 : $Builtin.FPIEEE32) : $Builtin.FPIEEE32 // users: %11, %4
  %4 = struct $Float (%3 : $Builtin.FPIEEE32)     // user: %14
  // function_ref closure #1 in _vjpSin(_:)
  %5 = function_ref @$vjpSin : $@convention(thin) (Float, Float) -> Float // user: %6
  %6 = partial_apply [callee_guaranteed] %5(%0) : $@convention(thin) (Float, Float) -> Float // user: %16
  %7 = builtin "int_cos_FPIEEE32"(%2 : $Builtin.FPIEEE32) : $Builtin.FPIEEE32 // users: %11, %8
  %8 = struct $Float (%7 : $Builtin.FPIEEE32)     // user: %14
  // function_ref closure #1 in _vjpCos(_:)
  %9 = function_ref @$vjpCos : $@convention(thin) (Float, Float) -> Float // user: %10
  %10 = partial_apply [callee_guaranteed] %9(%0) : $@convention(thin) (Float, Float) -> Float // user: %16
  %11 = builtin "fmul_FPIEEE32"(%3 : $Builtin.FPIEEE32, %7 : $Builtin.FPIEEE32) : $Builtin.FPIEEE32 // user: %12
  %12 = struct $Float (%11 : $Builtin.FPIEEE32)   // user: %17
  // function_ref closure #1 in static Float._vjpMultiply(lhs:rhs:)
  %13 = function_ref @$vjpMultiply : $@convention(thin) (Float, Float, Float) -> (Float, Float) // user: %14
  %14 = partial_apply [callee_guaranteed] %13(%8, %4) : $@convention(thin) (Float, Float, Float) -> (Float, Float) // user: %16
  // function_ref pullback of g(_:)
  %15 = function_ref @$pullback_g : $@convention(thin) (Float, @owned @callee_guaranteed (Float) -> Float, @owned @callee_guaranteed (Float) -> Float, @owned @callee_guaranteed (Float) -> (Float, Float)) -> Float // user: %16
  %16 = partial_apply [callee_guaranteed] %15(%6, %10, %14) : $@convention(thin) (Float, @owned @callee_guaranteed (Float) -> Float, @owned @callee_guaranteed (Float) -> Float, @owned @callee_guaranteed (Float) -> (Float, Float)) -> Float // user: %17
  %17 = tuple (%12 : $Float, %16 : $@callee_guaranteed (Float) -> Float) // user: %18
  return %17 : $(Float, @callee_guaranteed (Float) -> Float) // id: %18
}
